OUTPUT_FORMAT("elf32-h8300")
OUTPUT_ARCH(h8300h)
ENTRY("_start")

MEMORY
{
	romall(rx)	: o = 0x000000, l = 0x080000	/* 512KB */
	vectors(r)	: o = 0x000000, l = 0x000100	/* top of ROM */
	rom(rx)		: o = 0x000100, l = 0x07ff00

	ramall(rwx)	: o = 0xffbf20, l = 0x004000	/* 16KB */
	data(rwx)	: o = 0xfffc20, l = 0x000300
	stack(rw)	: o = 0xffff00, l = 0x000000	/* end of RAM */
}

SECTIONS
{
	.vectors : {
		vector.o(.data)
	} > vectors

	.text : {
		_text_start = . ;
		*(.text.startup)
		*(.text)
		_etext = . ;
	} > rom

	.rodata : {
		_rodata_start = . ;
		*(.strings)
		*(.rodata)
		*(.rodata.*)
		_erodata = . ;
	} > rom

	.data : {
		_data_start = . ;
		*(.data)
		_edata = . ;
	} > data AT> rom	/* .data セクションの「VA≠PA」対応 */
	_data_start_load = LOADADDR(.data);	/* data領域先頭の物理アドレスを取得する */

	.bss : {
		_bss_start = . ;
		*(.bss)
		*(COMMON)
		_ebss = . ;
	} > data AT> rom	/* .bss セクションの「VA≠PA」対応 */

	. = ALIGN(4);
	_end = . ;

	.stack : {
		_stack = . ;
	} > stack
}
